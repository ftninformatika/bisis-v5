apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.gliderpilot.gradle.jnlp:gradle-jnlp-plugin:+'
    }
}
apply plugin: 'de.gliderpilot.jnlp'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "5.0.0"

repositories {
    maven { url "https://clojars.org/repo" }
    maven { url "https://maven.repository.redhat.com/ga/" }
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/"}
    mavenCentral()
}

dependencies {
    compile project(":bisis-core")
    compile group: 'net.sf.jasperreports', name: 'jasperreports-fonts', version: '6.0.0'
    compile group: 'com.miglayout', name: 'miglayout-swing', version: '5.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile group: 'xalan', name: 'xalan', version: '2.7.2'
    compile 'com.squareup.retrofit2:converter-jackson:2.3.0'
    compileOnly('org.projectlombok:lombok:1.16.16')
    compile group: 'commons-logging', name: 'commons-logging', version: '1.1.1'
    compile group: 'dom4j', name: 'dom4j', version: '1.6.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'
    compile group: 'com.jgoodies', name: 'jgoodies-common', version: '1.6.0'
    compile group: 'com.jgoodies', name: 'forms', version: '1.3.0'
    compile group: 'com.jgoodies', name: 'looks', version: '2.2.2'
    compile group: 'com.toedter', name: 'jcalendar', version: '1.4'
    compile group: 'com.seaglasslookandfeel', name: 'seaglasslookandfeel', version: '0.2.1'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
    compile group: 'stax', name: 'stax-api', version: '1.0.1'
    compile group: 'net.sf.jasperreports', name: 'jasperreports', version: '6.4.3'
    compile fileTree(dir: '../old-libs', include: '*.jar')
    compile group: 'com.lowagie', name: 'itext', version: '2.1.7'
}


jar {
    mainClassName = 'com.ftninformatika.bisis.BisisApp'
    manifest {
        attributes 'Implementation-Title': 'Bisis Client App',
                'Implementation-Version': version,
                'Main-Class': 'com.ftninformatika.bisis.BisisApp',
                'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
    }
}

// koristi se ovako: gradle createWebstartDir
jnlp {
    useVersions = true
    usePack200 = false
    withXml {
        information {
            title 'BISIS aplikacija'
            vendor 'FTN Informatika'
        }
        shortcut {
            desktop()
            menu (submenu: 'BISIS')
        }
        security {
            'all-permissions'()
        }
    }

    href 'bisis5.jnlp'
    codebase 'https://app.bisis.rs/instalacija'
    signJarParams = [
            keystore : new File(project.rootDir, "bisis-key/FTNInformatika2017.jks"),
            alias    : 'ftninformatika',
            storepass: 'bisis',
            tsaurl   : 'http://timestamp.digicert.com']
}




task compileJasperJava(dependsOn: 'compileJava') {
    doLast {
        def jasperSourceDir = file('src/main/resources/jaspers')
        def jasperTargetDir = file('build/resources/main/jaspers')
        def jasperTargetDirSrc = file('src/main/resources/jaspers')
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: sourceSets.main.compileClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDir)
                include(name: '**/*.jrxml')
            }
            jasperTargetDirSrc.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDirSrc) {
                classpath(path: sourceSets.main.output.classesDir)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.dependsOn compileJasperJava